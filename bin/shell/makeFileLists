#! /bin/csh -f

# Sun-$Revision: 30.8 $

# Copyright 1992-2012 AUTHORS.
# See the LICENSE file for license information. 

# This shell script creates the make files for Self.

set nonomatch

set dir = `cwdRootedIn $SELF_WORKING_DIR`

set arch=`get_arch`
if ("$dir" != "vm/$arch/generated") then
   echo error: should only be executed in $arch/generated
   exit 1
endif

onintr cleanup

if (! $?MAKE) set MAKE = gmake

set dep  = /tmp/Dependencies$$
set allobjs = /tmp/allobjs$$

rm -f $dep $allobjs
rm -f Objs* Tags *~ 
rm -rf deps incls/_*.incl
mkdir deps

foreach dir ($*)
  echo
  echo makeFileLists entering $dir "(trying make rcsFileList)"
  pushd $SELF_WORKING_DIR/vm/src/$dir > /dev/null
  set files = (`$MAKE -s rcsFileList`)
  popd > /dev/null
  set objs = Objs.$dir:t
  rm -f $objs
  touch $objs
  foreach file ($files)
    set ext  = $file:e
    set base = $file:r
    if (-e $SELF_WORKING_DIR/vm/src/$dir/$file) then
      echo $file >> Tags
      if ($ext == c || $ext == cpp || $ext == s || $ext == S) then
        echo ${base}.o >> $objs
        if ($ext == c  || $ext == cpp) then 
          set d = deps/_${base}.${ext}.dep
          endif
      endif
    endif
  end
  cat $objs >> $allobjs
end

echo Finishing up dependencies

echo '#include' \"Dependencies.hh\"            >> $dep
echo ''                                        >> $dep
echo '${AOUT} files: ${PATCHED_OBJS}'          >> $dep
echo '${AOUT}:' `cat $allobjs` '${GLUE_LIBS}'  >> $dep
echo -n '        @lock_run _$@.lock' "'"       >> $dep
echo ' echo Compiling vmDate ; \'              >> $dep
echo '        make_vmDate; ${AS} ${ASFLAGS} vmDate.s -o vmDate.o; \'>> $dep
echo '        check_locks ; \'                 >> $dep
echo '        echo Linking $@ ; \'             >> $dep
echo '        rm -rf $@ cleanLocks; \'         >> $dep   # if disk space is  tight...
echo '        ${LD} -o $@' `cat $allobjs` '${PATCHED_OBJS}' '${LIBS}' "'"  >> $dep
echo ''                                        >> $dep
echo files: `cat $allobjs`                     >> $dep
echo ''                                        >> $dep

rm -f Dependencies.cpp
mv $dep Dependencies.cpp

done:
cleanup:
rm -f $dep $allobjs

exit
