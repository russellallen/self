#! /bin/csh -f
# Sun-$Revision: 30.8 $

# Copyright 1992-2006 Sun Microsystems, Inc. and Stanford University.
# See the LICENSE file for license information. 

# does a make files in the directory named by the first argument
#   (debug, optimized, or profiled) -- dmu 9/91

# extended to default to the current, then the debug directory.
# extended to use a separate command to find the link machine, 
# the normal compile machines, and the niced compile machines.
# changed how the output prints.
#   -- cdc 11/91

if (! $?MAKE) set MAKE = make

# parse arguments
if ($#argv == 0) then
    set d = ${cwd:t}
    if ($d == optimized) then
	set dir = $d
    else if ($d == profiled) then
	set dir = $d
    else
	set dir = debug
    endif
else if ($#argv == 1) then
    set dir = $1
else
    echo "usage: $0 [debug|optimized|profiled]"
    exit -1
endif

set OS=`get_arch`


# start the remote compiles
set cm=(`compileMachineNames`)
foreach m ($cm)
    echo -n "Compiling on $m "
    rsh $m "cd $SELF_WORKING_DIR/vm/$OS/$dir; $MAKE files | tee out.$m; \
	echo done >> out.$m; echo $m done" &
end

# start the remote compiles on other people's machines (niced!)
set ncm=(`nicedCompileMachineNames`)
foreach m ($ncm)
    echo -n "Compiling (niced) on $m "
    rsh $m "setenv SELF_LOAD_LIMIT 0.5; \
	cd $SELF_WORKING_DIR/vm/$OS/$dir; nice +12 $MAKE files | tee out.$m; \
	echo done >> out.$m; echo $m done" &
end

# start the link
set lms=(`linkMachineName`)
if ($#lms == 0) then
    # do nothing
else if ($#lms == 1) then
    set nlms=(`nicedLinkMachineName`)
    if ($#nlms != 0) then
        echo only one link machine allowed
	exit -1
    endif
    set m = $lms[1]
    echo "Linking on $m"
    rsh $m "cd $SELF_WORKING_DIR/vm/$OS/$dir; $MAKE Self | tee out.$m; \
        echo done >> out.$m; echo $m done"
else
    echo only one link machine allowed
    exit -1
endif

# start the link on another person's machine (niced!)
set nlms=(`nicedLinkMachineName`)
if ($#nlms == 0) then
    if ($#lms == 0) then
        echo should have at least one link machine
	exit -1
    endif
    # do nothing
else if ($#nlms == 1) then
    if ($#lms == 1) then
        echo only one link machine allowed
	exit -1
    endif
    set m = $nlms[1]
    echo "Linking (niced) on $m"
    rsh $m "setenv SELF_LOAD_LIMIT 0.5; \
	cd $SELF_WORKING_DIR/vm/$OS/$dir; nice +12 $MAKE Self | tee out.$m; \
	echo done >> out.$m; echo $m done"
else
    echo only one link machine allowed
    exit -1
endif
