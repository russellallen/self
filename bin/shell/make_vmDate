#!/bin/csh

# Sun-$Revision: 30.9 $

# constructs vmDate.s

set file="vmDate.s"

set arch=`get_arch`

if ($arch == mac_osx ) then 
  set cpu=`arch`
  if ($cpu == ppc) then
    set arch="Mac OSX"
  else if ($cpu == i386) then
    set arch="Mac OSX I386"
  else
    set arch="Mac OSX 64bit" 
    #not there yet
  endif
  set label="_vmDate"
  set data=".const"
  set ascii=".asciz"
  set comment="#"
  set global=".globl"
  set word=".long"
else if ( $arch == linux ) then
  set arch="Linux"
  set label="vmDate"
  set data=".data"
  set ascii=".asciz"
  set comment="#"
  set global=".globl"
  set word=".long"
else if ( $arch == svr4 ) then 
  set arch="Solaris 2.x (svr4)"
  set label="vmDate"
  set data=".data"
  set ascii=".ascii"
  set comment="\!"
  set global=".global"
  set word=".word"
else 
  set arch="SunOS 4.1 (sun4)"
  set label="_vmDate"
  set data=".data"
  set ascii=".ascii"
  set comment="!"
  set global=".global"
  set word=".word"
endif

rm -f $file
touch $file
echo "$comment generated by $0 -- do not change by hand "	>> $file
echo "$data"                                       >> $file
echo "$global $label "	 				>> $file
echo "LC0: "						>> $file
echo "$ascii  "'"'"`date '+%a %d %h %y %H:%M:%S'` $arch"'"' 	>> $file
echo ".align 4 "					>> $file
echo "${label}: $word LC0"				>> $file
exit 0
