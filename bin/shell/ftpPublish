#! /bin/csh -f

# Sun-$Revision: 30.6 $

# Copyright 1992-2012 AUTHORS.
# See the LICENSE file for license information. 

set args = ($*)
if ($#args == 0) goto usage

set nonomatch

set subdir = `cwdRootedIn $SELF_WORKING_DIR`
if ( $status != 0) then
  exit 1
endif

set executables = ()

while ($#args > 0)
    switch ("$args[1]")
	case "-x":
	    # executable files to be copied are also stripped
	    shift args
	    if ($#args == 0) goto usage
            set executables = ($executables $args[1])
	    breaksw
	case "-d":
	    # Specify base directory for ftp
	    shift args
	    if ($#args == 0) goto usage
	    set dir = $args[1]
	    breaksw
	case "-v":
	    # verbose: print out the logic of the publish
	    set print
	    breaksw
	default:
	    # not a switch; all remaining arguments are files to be published
            break
    endsw
    shift args
end

set files = ($args)

echo "Publishing to ftp directory ${dir} in sub directory $subdir"

set ftpdir = $dir/$subdir

if ($?print) then
  echo "executables: " $executables
  echo "files:       " $files
endif

# Check if files and  exist
foreach workfile ($files $executables)
    if (! -e $workfile) then
	echo "warning: $workfile does not exist; ignoring"
    endif
end

# Create needed directory path
set tmp     = $subdir
set dirlist = (${tmp:t})
while (${tmp:h} != ${tmp:t})
  set tmp = ${tmp:h}
  set dirlist = (${tmp:t} $dirlist)
end

set tmpdir = $dir
foreach tmp ($dirlist)
    set tmpdir = $tmpdir/$tmp
    if (! -e $tmpdir) then
        echo "Creating directory $tmpdir"
        mkdir $tmpdir
    endif
end

set lockfile = $ftpdir/.lock
onintr cleanup

set oldmask = `umask`
umask 222
echo -n $user is publishing > /tmp/$$
if (-e $lockfile) then
	cat $lockfile
	echo "; please try again later."
	umask $oldmask
	exit
else 
        cp /tmp/$$ $lockfile >& /dev/null
endif
umask $oldmask

if (! { cp /dev/null ${ftpdir}/.test >& /dev/null }) then
    echo "You don't have write permission in ${ftpdir}."
    echo "Please do 'su release' before running this script."
    exit
else
    rm ${ftpdir}/.test
endif

echo Removing old ftp files in $ftpdir
rm -f $ftpdir/*

foreach workfile ($files)
    set basefile = $ftpdir/$workfile
    echo Copying $workfile
    cp $workfile $basefile  
    if ( -x $workfile ) then
      chmod 755 $basefile
    else   
      chmod 644 $basefile
    endif
end

foreach workfile ($executables)
    set basefile = $ftpdir/$workfile
    echo Copying $workfile
    cp $workfile $basefile
    echo Stripping $basefile
    strip $basefile
    chmod 755 $basefile
end

echo Done.

cleanup:
    rm -f $lockfile /tmp/$$
    if ($?tmpfile) then
	rm -f $tmpfile
    endif
    exit

usage:
    echo "usage: ftpPublish [-v] [-d targetdir] {-x executable-file}* {file}*"
    exit
