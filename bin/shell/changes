#! /bin/csh -f

# Sun-$Revision: 30.8 $

# Copyright 1992-2006 Sun Microsystems, Inc. and Stanford University.
# See the LICENSE file for license information. 

# checks to see which files in the current directory are different from
# those in the baseline directory

set basedir = $SELF_BASELINE_DIR
set rootdir = $SELF_WORKING_DIR

set arch=`get_arch`

# must set path so doesn't use linked from this dir
set path=($basedir/bin/{$arch,shell} $path)

while ($#argv > 0)
    switch ("$1")
	case "-d":
	    # set root directory name
	    shift
	    if ($#argv == 0) goto usage
	    set rootdir = $1
	    breaksw
	case "-h":
	    # print out usage information
	    goto usage
	case "-?":
	    # ignore all other switches
	    echo "warning: switch $1 ignored"
	    breaksw
	default:
	    # unexpected argument
	    echo "unexpected argument $1; aborting"
	    goto usage
    endsw
    shift
end

if (! -d $rootdir) then
    echo "$rootdir is not a directory; aborting"
    exit -1
endif
set subdir = `cwdRootedIn $rootdir`
if ($status != 0) then
  exit 1
endif

set dir = $basedir/$subdir

set lockfile = $dir/.lock

echo -n $user is checking for changes > /tmp/$$
if (! { cp /tmp/$$ $lockfile }) then
	rm -f /tmp/$$
	cat $lockfile
	echo "; please try again later."
	exit
endif
rm -f /tmp/$$

onintr cleanup

if (! -d $dir/RCS) then
	echo "Can't find RCS directory $dir/RCS"
	goto cleanup
endif

set nonomatch
set curdir = `pwd`
cd $dir/RCS; set src = `echo *,v | sed -e "s/,v//g"`; cd $curdir
unset nonomatch
foreach f ($src)
    set basefile = $dir/$f
    if (-e $f && ! { cmp -s $f $basefile }) then
	echo "$f differs from $basefile"
    endif
end

cleanup:
    rm -f $lockfile
    exit

usage:
    echo "usage: changes [-d rootdir]"
    echo "   or: changes -h"
