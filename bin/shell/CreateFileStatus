#! /bin/csh -f

# Sun-$Revision: 30.8 $

# Copyright 1992-2012 AUTHORS.
# See the LICENSE file for license information. 

# Create the file .file_status in the basline directory corresponding to cwd.

onintr cleanup

set nonomatch

set basedir = $SELF_BASELINE_DIR
set rootdir = $SELF_WORKING_DIR

set arch=`get_arch`

# must set path so doesn't use cwdRootedIn from this dir
set path=($rootdir/bin/{$arch,shell} $basedir/bin/{$arch,shell} $path)

while ($#argv > 0)
    switch ("$1")
	case "-d":
	    # set root directory name
	    shift
	    if ($#argv == 0) goto usage
	    set rootdir = $1
	    breaksw
	case "-h":
	    # print out usage information
	    goto usage
	case "-?":
	    # ignore all other switches
	    echo "warning: switch $1 ignored"
	    breaksw
	default:
	    # unexpected argument
	    echo "unexpected argument $1; aborting"
	    goto usage
    endsw
    shift
end

if (! -d $rootdir) then
    echo "$rootdir is not a directory; aborting"
    exit -1
endif
set subdir = `cwdRootedIn $rootdir`
if ($status != 0) then
    exit -1
endif

set dir = $basedir/$subdir

if (! -d $dir/RCS) then
    echo "$dir/RCS does not exist; aborting"
    exit -1
endif

set outfile = $dir/.file_status

# echo "Creating ${outfile}."

set files = `(ls $dir;ls $dir/RCS|sed -e "s|,v||") | sort -u`

rm -f $outfile

echo \"Generated by CreateFileStatus \(`date`\)\" > $outfile

foreach workfile ($files)
    set basefile = $dir/$workfile
    set baseRCSfile = $dir/RCS/$workfile,v
    if (-f $basefile && -f $baseRCSfile) then
	set baserev = `getRevision $basefile`
	if ("$baserev" == "") then
	    set baserev = none
	endif
	echo $workfile RCS $baserev >> $outfile
    else if (-f $basefile) then
	echo $workfile Copy >> $outfile
    else if (-f $baseRCSfile) then
	echo $workfile Removed >> $outfile
    endif
end

exit

cleanup:
# Remove the incomplete result
if (-e ${outfile}) then
    echo Removing ${outfile}
    mv ${outfile}
endif
exit

usage:
    echo "usage: CreateFileStatus [-d rootdir]"
    echo "   or: CreateFileStatus -h"
